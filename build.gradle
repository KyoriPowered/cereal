plugins {
  id 'java'
  id 'maven'
  id 'signing'
  id 'net.minecrell.licenser' version '0.3'
}

group 'net.kyori'
version '1.0.3-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

license {
  header project.file('header.txt')
  include '**/*.java'
  newLine false
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    name 'sonatype-oss'
    url 'https://oss.sonatype.org/content/groups/public/'
  }
}

dependencies {
  compile 'com.github.ben-manes.caffeine:caffeine:2.6.0'
  compile 'net.kyori:blizzard:1.0.4-JDK8144185'
  compile 'net.kyori:gson:2.8.3-SNAPSHOT'
  compile 'net.kyori:lunar:1.0.3'
  compile 'org.ow2.asm:asm-debug-all:5.2'
  testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
  classifier 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') && project.hasProperty('signing.keyId') }
  sign configurations.archives
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'net.kyori.cereal'
  )
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
  uploadArchives {
    enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          description 'A document generation library for use with Gson.'
          name project.name
          url 'https://github.com/KyoriPowered/cereal/'

          developers {
            developer {
              name 'kashike'
            }
          }

          issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/KyoriPowered/cereal/issues'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          scm {
            connection 'scm:git@github.com:KyoriPowered/cereal.git'
            developerConnection 'scm:git@github.com:KyoriPowered/cereal.git'
            url 'https://github.com/KyoriPowered/cereal/'
          }
        }
      }
    }
  }
}
